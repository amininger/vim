
""""""""""""""""""" GENERAL FUNCTIONS """""""""""""""""""


"""""
" TrimString(str:string)
"   Returns a copy of the string with whitespace 
"   at the beginning and end of the string removed

function! TrimString(str)
  return substitute(a:str, '^\s*\(.\{-}\)\s*$', '\1', '')
endfunction

""""""""""""""""""" OPENING GREPPED FILES """""""""""""""""""
" These functions are for using grep inside vim and opening
"   each file in a split pane above the :copen pane


"""""
" GetEditCommandForFile()
"   Returns a command to edit the file at the line number
"   Specified in the current line in the :copen buffer

function! GetEditCommandForFile()
	let line_arr = split(getline('.'), "|")
	let filename = line_arr[0]
	let lineno = line_arr[1]
	return "e +".lineno." ".filename
endfunction
	

"""""
" OpenPrevFileInWindow()
"   Will move the cursor to the previous line in the copen buffer
"   and open that file in the panel above this one

function! OpenPrevFileInWindow()
	wincmd j
	execute "normal! k"
	let editcmd=GetEditCommandForFile()
	wincmd k
	execute(editcmd)
endfunction


"""""
" OpenPrevFileInWindow()
"   Will move the cursor to the next line in the copen buffer
"   and open that file in the panel above this one

function! OpenNextFileInWindow()
	wincmd j
	execute "normal! j"
	let editcmd=GetEditCommandForFile()
	wincmd k
	execute(editcmd)
endfunction

""""""""""""""""""" FILE EXPLORER """""""""""""""""""
" These functions are used within the file explorer
"   To expand its functionality

"""""
" ExtractDirectoryName(line:string)
"   Given a line in the file explorer buffer
"   This will return the directory name
"   (Whitespace and arrow symbols removed)

function! ExtractDirectoryName(line)
  return TrimString(substitute(a:line, '▾\|▸', '', ''))
endfunction


"""""
" IsOpenDir(line:string)
"   Given a line in the file explorer buffer
"   Returns true if it represents an open directory

function! IsOpenDir(line)
  return (a:line =~ '▾')
endfunction


"""""
" IsClosedDir(line:string)
"   Given a line in the file explorer buffer
"   Returns true if it represents a closed directory

function! IsClosedDir(line)
  return (a:line =~ '▸')
endfunction


"""""
" IsDir(line:string)
"   Given a line in the file explorer buffer
"   Returns true if it represents a directory

function! IsDir(line)
  return IsOpenDir(a:line) || IsClosedDir(a:line)
endfunction


"""""
" ValidateFile(line:string)
"   Will try to make sure the given line is a valid file
"   Returns true if so, Returns false and prints error if not

function! ValidateFile(line)
  " If it is a directory, don't open
  if IsDir(a:line)
    echom "ValidateFile FAIL: Tried to open directory as file"
    return 0
  endif

  " If there is no indent, it is also not a file
  if indent(a:line) == 0
    echom "ValidateFile FAIL: Tried to open file at 0 indentation"
    return 0
  endif

  " If the line is empty, it is not a file
  if strlen(a:line) == 0
    echom "ValidateFile FAIL: Tried to open an empty file"
    return 0
  endif

  return 1
endfunction


"""""
" GetCurrentExplorerDirectory()
"   Given the cursor at a line in the file explorer buffer
"   This returns the fully qualified directory that cursor is at (if a dir)
"     or the directory the file is in (if a file)

function! GetCurrentExplorerDirectory()
  let line_no = line('.')
  let indent_level = indent(line_no)

  " Check if the cursor is at a directory
  let dir = ""
  if IsDir(getline('.'))
    let line = getline(line_no)
    let dir = ExtractDirectoryName(line)
  endif

  while indent_level > 2 && line_no > 0
    " Keep going up the file until we are out of the tree or at the top of the page
    let line_no = line_no - 1
    
    if (IsOpenDir(getline(line_no))) && (indent(line_no) + 4 == indent_level)
      " We are at an expanded directory at the proper indentation, add it to the dir list
      let folder = ExtractDirectoryName(getline(line_no))
      let dir = folder.dir
      let indent_level = indent_level - 2
    endif
  endwhile

  " Return the fully qualitifed directory
  return expand('%:p:h')."/".dir
endfunction


"""""
" OpenFileInTmuxPane()
"   If the cursor is on a file in the file explorer,
"   Then open that file in a vim server called tmux

function! OpenFileInTmuxPane()
  let line_no = line('.')
  let cur_line = TrimString(getline(line_no))

  if (!ValidateFile(cur_line))
    return
  endif

  let dir = GetCurrentExplorerDirectory()
  let filename = cur_line
  let file = dir.filename

  let opencmd=":silent !vim --servername tmux --remote-tab ".file
  execute opencmd | redraw!
endfunction


"""""
" CreateFileInCurrentDirectory(filename:string)
"   Creates a file with the given name in the 
"     current directory and opens it in tmux pane

function! CreateFileInCurrentDirectory(filename)
  let line_no = line('.')
  let cur_dir = expand('%:p:h')

  let dir = GetCurrentExplorerDirectory()
  let file = dir.a:filename

  " Create the file
  execute ":silent !touch ".file

  " Open it in the tmux tab
  execute ":silent !vim --servername tmux --remote-tab ".file

  " Redraw
  redraw!

  " Refresh the file"
  call feedkeys("r")
endfunction


"""""
" DeleteFileUnderCursor()
"   Deletes the file under the cursor

function! DeleteFileUnderCursor()
  let line_no = line('.')
  let cur_line = getline('.')
  let cur_dir = expand('%:p:h')

  " Make sure its a file
  if !ValidateFile(cur_line)
    return
  endif

  let dir = GetCurrentExplorerDirectory()
  let filename = TrimString(cur_line)
  let file = dir.filename

  " Remove the file
  execute ":silent !rm ".file
  
  " Redraw
  redraw!

  " Refresh the file"
  call feedkeys("kr")
endfunction
  
let g:netrw_fastbrowse=0


""""""""""""""""""" SOAR FUNCTIONS """""""""""""""""""


