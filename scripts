""""""""""""""""""" GENERAL FUNCTIONS """""""""""""""""""


"""""
" TrimString(str:string)
"   Returns a copy of the string with whitespace 
"   at the beginning and end of the string removed

function! TrimString(str)
  return substitute(a:str, '^\s*\(.\{-}\)\s*$', '\1', '')
endfunction


""""
" GetSelectedText()
"   Returns the currently selected text (from visual mode)
function! GetSelectedText()
    let [lnum1, col1] = getpos("'<")[1:2]
    let [lnum2, col2] = getpos("'>")[1:2]
    let lines = getline(lnum1, lnum2)
    let lines[-1] = lines[-1][: col2 - (&selection == 'inclusive' ? 1 : 2)]
    let lines[0] = lines[0][col1 - 1:]
    return join(lines, "\n")
endfunction


""""""""""""""""""" OPENING GREPPED FILES """""""""""""""""""
" These functions are for using grep inside vim and opening
"   each file in a split pane above the :copen pane


"""""
" GetEditCommandForFile()
"   Returns a command to edit the file at the line number
"   Specified in the current line in the :copen buffer

function! GetEditCommandForFile()
	let line_arr = split(getline('.'), "|")

    " Must have at least 2 parts (for the line number)
	if len(line_arr) < 2
		let line_arr = split(getline('.'), ":")
		if len(line_arr) < 2
			return ""
		endif
	endif

    " Don't open a binary file
    let filename = line_arr[0]
    if (filename =~ "|| Binary file")
        return ""
    endif

	let lineno = line_arr[1]
	if strlen(filename) == 0
		return ""
	endif
	return "e +".lineno." ".filename
endfunction
	
"""""
" OpenCurrentFileInWindow()
"   Will open the file at the current cursor location in the copen buffer

function! OpenCurrentFileInWindow()
	let editcmd=GetEditCommandForFile()
	wincmd k
	execute(editcmd)
endfunction

"""""
" OpenPrevFileInWindow()
"   Will move the cursor to the previous line in the copen buffer
"   and open that file in the panel above this one

function! OpenPrevFileInWindow()
	wincmd j
	execute "normal! k"
	let editcmd=GetEditCommandForFile()
	wincmd k
	execute(editcmd)
endfunction


"""""
" OpenNextFileInWindow()
"   Will move the cursor to the next line in the copen buffer
"   and open that file in the panel above this one

function! OpenNextFileInWindow()
	wincmd j
	execute "normal! j"
	let editcmd=GetEditCommandForFile()
	wincmd k
	execute(editcmd)
endfunction



